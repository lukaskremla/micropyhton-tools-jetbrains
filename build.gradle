buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.4.21'
}

apply plugin: 'kotlin'

intellij {
    version ideaVersion
    pluginName 'intellij-micropython'
    downloadSources Boolean.valueOf(downloadIdeaSources)
    updateSinceUntilBuild false
    plugins = ['terminal']
    if (ideaVersion.contains('PC')) {
        plugins += ['python-ce']
    } else if (ideaVersion.contains('PY')) {
        plugins += ['python']
    } else {
        plugins += [pythonPlugin]
    }
    publishPlugin {
        username publishUsername
        token publishToken
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
}

task copyStubs(dependsOn: prepareSandbox, type: Copy) {
    from(projectDir) {
        include "typehints/"
        include "scripts/"
    }
    into "${intellij.sandboxDirectory}/plugins/intellij-micropython"
}

buildPlugin.dependsOn copyStubs
runIde.dependsOn copyStubs
