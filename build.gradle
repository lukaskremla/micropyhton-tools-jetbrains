buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.4.10'
}

apply plugin: 'kotlin'

def isPyCharm = ideaVersion.contains('PY') || ideaVersion.contains('PC')

intellij {
    version ideaVersion
    pluginName 'intellij-micropython'
    downloadSources Boolean.valueOf(downloadIdeaSources)
    updateSinceUntilBuild false
    plugins = ['terminal']
    if (isPyCharm) {
        plugins += ['python-ce']
    } else {
        plugins += [pythonPlugin]
    }
    publishPlugin {
        username publishUsername
        token publishToken
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
}

task copyStubs(dependsOn: prepareSandbox, type: Copy) {
    from(projectDir) {
        include "typehints/"
        include "scripts/"
    }
    into "${intellij.sandboxDirectory}/plugins/intellij-micropython"
}

buildPlugin.dependsOn copyStubs
runIde.dependsOn copyStubs
